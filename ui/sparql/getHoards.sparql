PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dcterms:  <http://purl.org/dc/terms/>
PREFIX dcmitype:	<http://purl.org/dc/dcmitype/>
PREFIX nm:       <http://nomisma.org/id/>
PREFIX nmo:	<http://nomisma.org/ontology#>
PREFIX skos:      <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf:	<http://xmlns.com/foaf/0.1/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX osgeo:	<http://data.ordnancesurvey.co.uk/ontology/geometry/>

SELECT DISTINCT ?hoard ?hoardLabel ?closingDate ?place ?label ?lat ?long ?geojson WHERE {
BIND (<COINTYPE> as ?coinType)
{?object nmo:hasTypeSeriesItem|nmo:hasTypeSeriesItem/skos:exactMatch|nmo:hasTypeSeriesItem/skos:broader+ ?coinType ; 
  	a nmo:NumismaticObject ;
    dcterms:isPartOf ?hoard .
  ?hoard nmo:hasFindspot/crm:P7_took_place_at/crm:P89_falls_within ?place }
UNION { ?contents nmo:hasTypeSeriesItem ?coinType ;
                  a dcmitype:Collection .
  ?hoard dcterms:tableOfContents ?contents ;
    nmo:hasFindspot/crm:P7_took_place_at/crm:P89_falls_within ?place }
OPTIONAL {?hoard skos:prefLabel ?hoardLabel FILTER langMatches(lang(?hoardLabel), "en")}
OPTIONAL {?hoard nmo:hasClosingDate ?closingDate}
?place a crm:E53_Place ;
    rdfs:label ?label ;
    geo:location ?loc .
OPTIONAL {?loc geo:lat ?lat ; geo:long ?long}
OPTIONAL {?loc osgeo:asGeoJSON ?geojson}
}